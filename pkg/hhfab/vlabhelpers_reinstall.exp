#!/usr/bin/expect -f
# Copyright 2024 Hedgehog
# SPDX-License-Identifier: Apache-2.0

set force_conservative 1
set timeout -1

# Initialize variables
set WAIT_READY 0
set remaining_args {}

# Parse arguments
for {set i 0} {$i < [llength $argv]} {incr i} {
    set arg [lindex $argv $i]
    switch -- $arg {
        "--wait-ready" {
            set WAIT_READY 1
        }
        default {
            lappend remaining_args $arg
        }
    }
}

# Reassign the remaining arguments
set argv $remaining_args

if {[llength $argv] == 1} {
	puts "No credentials are provided, assume external power reset"
	lassign $argv SW_NAME
	set POWER_RESET 1
} elseif {[llength $argv] == 3} {
	puts "Credentials are provided, will attempt reboot"
	lassign $argv SW_NAME USER PASSWORD
	set POWER_RESET 0
} else {
	puts "Usage: $argv0 SW_NAME \[USER\] \[PASSWORD\] \[--wait-ready\]"
	exit 1
}

# Define ANSI escape codes for colors
set GREY "\033\[38;5;242m"
set INF "\033\[0;92m"
set ERR "\033\[38;5;196m"
set RESET "\033\[0m"
set LOG_BG "\033\[49m"

# Helper function to send colorized output to stderr
proc log_message {loglevel SW_NAME msg} {
	set current_time [clock format [clock seconds] -format "%H:%M:%S"]

	if {$loglevel == "INF"} {
		puts stderr "$::LOG_BG$::GREY$current_time$::RESET $::INF$loglevel$::RESET $SW_NAME: $msg"
	} elseif {$loglevel == "ERR"} {
		puts stderr "$::LOG_BG$::GREY$current_time$::RESET $::ERR$loglevel$::RESET $SW_NAME: $msg"
	} else {
		puts stderr "$::LOG_BG$::GREY$current_time$::RESET $SW_NAME: $msg"
	}
}

set KEY_UP   "\033\[A"
set KEY_DOWN "\033\[B"
set KEY_HOME "\033\[H"
set KEY_PGUP "\033\[5~"
set ONIE_HIGHLIGHT "*ONIE"
set ONIE_CHAIN_INSTALL "*ONIE: Install OS"

set ERROR_CONSOLE 1
set ERROR_LOGIN 2
set ERROR_INSTALL 3
set ERROR_HHFAB 4
set ERROR_UNKNOWN 5

# Check if HHFAB_BIN is set in the environment
if {[info exists env(HHFAB_BIN)]} {
	set HHFAB $env(HHFAB_BIN)
} else {
	# Fallback to look for hhfab in the local folder or in PATH
	if {[file exists "./hhfab"]} {
		set HHFAB "./hhfab"
	} else {
		catch {set HHFAB [exec which hhfab]} result
		if {[string length $result] == 0} {
			puts "Error: hhfab binary not found in HHFAB_BIN, current directory, or PATH."
			exit 1
		}
		set HHFAB $result
	}
}

# Verify that HHFAB contains a valid path
if {!([file exists $HHFAB] && [file executable $HHFAB])} {
	log_message "ERR" $SW_NAME "HHFAB does not contain a valid path or is not executable."
	exit $ERROR_HHFAB
}

# connect to the serial console of the switch with hhfab
puts "connecting to serial of $SW_NAME via hhfab serial..."
set spawn_result [catch {spawn $HHFAB vlab serial -n $SW_NAME} spawn_error]
if {$spawn_result != 0} {
	log_message "ERR" $SW_NAME "$HHFAB Failed with: $spawn_error"
	exit $ERROR_HHFAB
}

expect {
	-ex "Type the hot key to suspend the connection: <CTRL>Z" {
		send "\r"
	}
	-ex "Use Ctrl+] to escape, if no output try Enter, safe to use Ctrl+C/Ctrl+Z" {
		send "\r"
	}
	-ex "The connection was unsuccessful" {
		exit $ERROR_CONSOLE
	}
	timeout {
		log_message "ERR" $SW_NAME "Connection to console timed out"
		exit $ERROR_CONSOLE
	}
	eof {
		log_message "ERR" $SW_NAME "Connection to console failed"
		exit $ERROR_CONSOLE
	}
	default {
		log_message "ERR" $SW_NAME "Unexpected output: [expect_out(buffer)]"
		exit $ERROR_UNKNOWN
	}
}

if { ! $POWER_RESET} {
	set timeout 100
	expect {
	# handle wrong user/password
	-ex "Login incorrect" {
		exit $ERROR_LOGIN
	}
	# handle user login if not already logged in
	-re "$SW_NAME login:" {
		send "$USER\r"
		exp_continue
	}
	-ex "Password:" {
		send "$PASSWORD\r"
		exp_continue
	}
	# reboot in case we are at the prompt - eventually this will be done via PDU
	-ex "admin@$SW_NAME:~$" {
		log_message "INF" $SW_NAME "Rebooting..."
		send "sudo reboot\r"
	}
	# same for rescue mode
	-ex "ONIE:/ #" {
		send "reboot\r"
		sleep 5
	}
	timeout {
		log_message "ERR" $SW_NAME "Login to NOS timed out"
		exit $ERROR_LOGIN
	}
	eof {
		log_message "ERR" $SW_NAME "Login to NOS failed (EOF)"
		exit $ERROR_LOGIN
	}
	default {
		log_message "ERR" $SW_NAME "Unexpected output: [expect_out(buffer)]"
		exit $ERROR_UNKNOWN
	}
	}
}

expect -timeout 300 -ex "GNU GRUB" {
	log_message "INF" $SW_NAME "GRUB Menu detected"
	# Select the ONIE option, and finally the Install OS option
	sleep 1
	send -- "$KEY_HOME"
	set timeout 1
	expect {
		-ex $ONIE_HIGHLIGHT {
			set timeout -1
			send "\r"
			expect -ex "GNU GRUB"
			send -- "$KEY_HOME"
			sleep 1
			send "\r"
			expect {
				"ONIE: OS Install Mode ..." {
					log_message "INF" $SW_NAME "Entering OS Install Mode..."
				}
				timeout {
					send "\r"
				}
			}
		}
		timeout {
			send -- "$KEY_DOWN"
			exp_continue
		}
	}
} timeout {
	log_message "ERR" $SW_NAME "Failed to reset. GRUB not detected."
	exit $ERROR_INSTALL
}

if {$WAIT_READY} {
	set install_success 0
	# Wait for confirmation of the Install OS option
	expect -timeout 60 -ex "Starting ONIE Service Discovery" {
		# Wait for successful NOS installation
		expect -timeout 300 -ex "ONIE: NOS install successful" {
			log_message "INF" $SW_NAME "NOS installed successfully."
			set install_success 1
		} timeout {
			log_message "ERR" $SW_NAME "Failed to install NOS within 5 minutes from service discovery."
		}
	} timeout {
		log_message "ERR" $SW_NAME "Failed to select install mode."
	}

	# Exit if install failed
	if {$install_success == 0} {
		exit $ERROR_INSTALL
	}

	# Wait for the login prompt
	expect -timeout 300 -ex "sonic login:" {
		log_message "INF" $SW_NAME "Sonic login prompt detected."
	} timeout {
		log_message "ERR" $SW_NAME "Login prompt not detected within 5 minutes."
		exit $ERROR_INSTALL
	}

	# Wait for the "System is ready" message
	expect -timeout 300 -ex "System is ready" {
		log_message "INF" $SW_NAME "System is ready message detected."
		exit 0
	} timeout {
		log_message "ERR" $SW_NAME "System did not become ready within 5 minutes."
		exit $ERROR_INSTALL
	}
} else {
	# Skip readiness check
	exit 0
}
